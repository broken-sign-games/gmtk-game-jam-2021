title Unlink Mouse Ver
author Zachary Barbanell
homepage www.puzzlescript.net

verbose_logging

mouse_left
mouse_right
mouse_drag
mouse_up

run_rules_on_level_start

(noundo)

========
OBJECTS
========

lmb
pink

rmb
pink

lmbup
pink

rmbup
pink

drag
pink


Background
gray

CursorActiveStill
red
00.00
0...0
.....
0...0
00.00

CursorActiveMoved
red
00.00
0...0
.....
0...0
00.00

Tweak
orange
.....
..0..
.0.0.
..0..
.....

Wall
black
.0.0.
0...0
.....
0...0
.0.0.

Crate
orange
00000
0...0
0...0
0...0
00000

CrateTarget
Purple
.....
.000.
.0.0.
.000.
.....

CrossTarget
Purple
.0.0.
00.00
.....
00.00
.0.0.

HighUp
white
..0..
..0..
..0..
.....
.....

LowUp
white
..0..
.....
.....
.....
.....

HighDown
white
.....
.....
..0..
..0..
..0..


LowDown
white
.....
.....
.....
.....
..0..

HighLeft
white
.....
.....
000..
.....
.....

LowLeft
white
.....
.....
0....
.....
.....

HighRight
white
.....
.....
..000
.....
.....

LowRight
white
.....
.....
....0
.....
.....

HighUp_v1
lightblue blue darkblue
..0..
..1..
..2..
.....
.....

LowUp_v1
lightblue blue darkblue
..0..
.....
.....
.....
.....

HighDown_v1
lightblue blue darkblue
.....
.....
..2..
..0..
..1..


LowDown_v1
lightblue blue darkblue
.....
.....
.....
.....
..1..

HighLeft_v1
lightblue blue darkblue
.....
.....
012..
.....
.....

LowLeft_v1
lightblue blue darkblue
.....
.....
0....
.....
.....

HighRight_v1
lightblue blue darkblue
.....
.....
..201
.....
.....

LowRight_v1
lightblue blue darkblue
.....
.....
....1
.....
.....

HighUp_v2
lightred red darkred
..0..
..1..
..2..
.....
.....

LowUp_v2
lightred red darkred
..0..
.....
.....
.....
.....

HighDown_v2
lightred red darkred
.....
.....
..2..
..0..
..1..


LowDown_v2
lightred red darkred
.....
.....
.....
.....
..1..

HighLeft_v2
lightred red darkred
.....
.....
012..
.....
.....

LowLeft_v2
lightred red darkred
.....
.....
0....
.....
.....

HighRight_v2
lightred red darkred
.....
.....
..201
.....
.....

LowRight_v2
lightred red darkred
.....
.....
....1
.....
.....

Overlay (for putting on tiles with both high and low wires) (used to be visual, is now purely for logic)
transparent

WinConCrates
transparent

WinConCrosses
transparent

Temp
pink

move34lowertag
pink

move56alttag
transparent

=======
LEGEND
=======

. = Background
# = Wall
* = CrateTarget
+ = CrossTarget
O = Crate
1 = Wall and WinConCrates
2 = Wall and WinConCrosses

Player = Background (necessary for language reasons)

WinCon = WinConCrates or WinConCrosses
Target = CrossTarget or CrateTarget
Obstacle = Wall or Crate

CursorActive = CursorActiveStill or CursorActiveMoved

High = HighUp or HighDown or HighLeft or HighRight
Low = LowUp or LowDown or LowLeft or LowRight

Knot = High or Low

Up_ = HighUp or LowUp
Down_ = HighDown or LowDown
Left_ = HighLeft or LowLeft
Right_ = HighRight or LowRight

Horizontal_ = Left_ or Right_
Vertical_ = Up_ or Down_

HighHorizontal = HighLeft or HighRight
HighVertical = HighUp or HighDown

LowHorizontal = LowLeft or LowRight
LowVertical = LowUp or LowDown

Left_v1 = LowLeft_v1 or HighLeft_v1
Right_v1 = LowRight_v1 or HighRight_v1
Up_v1 = LowUp_v1 or HighUp_v1
Down_v1 = LowDown_v1 or HighDown_v1

Low_v1 = LowUp_v1 or LowDown_v1 or LowLeft_v1 or LowRight_v1
High_v1 = HighUp_v1 or HighDown_v1 or HighLeft_v1 or HighRight_v1

Left_v2 = LowLeft_v2 or HighLeft_v2
Right_v2 = LowRight_v2 or HighRight_v2
Up_v2 = LowUp_v2 or HighUp_v2
Down_v2 = LowDown_v2 or HighDown_v2

Low_v2 = LowUp_v2 or LowDown_v2 or LowLeft_v2 or LowRight_v2
High_v2 = HighUp_v2 or HighDown_v2 or HighLeft_v2 or HighRight_v2

LowUpDeco = LowUp_v1 or LowUp_v2
LowDownDeco = LowDown_v1 or LowDown_v2
LowLeftDeco = LowLeft_v1 or LowLeft_v2
LowRightDeco = LowRight_v1 or LowRight_v2
HighUpDeco = HighUp_v1 or HighUp_v2
HighDownDeco = HighDown_v1 or HighDown_v2
HighLeftDeco = HighLeft_v1 or HighLeft_v2
HighRightDeco = HighRight_v1 or HighRight_v2

LowDeco = Low_v1 or Low_v2
HighDeco = High_v1 or High_v2
Decoration = LowDeco or HighDeco


╂ = HighUp and HighDown and Overlay and LowLeft and LowRight
┿ = HighLeft and HighRight and Overlay and LowUp and LowDown

│ = HighUp and HighDown
─ = HighLeft and HighRight
┘ = HighUp and HighLeft
└ = HighUp and HighRight
┐ = HighDown and HighLeft
┌ = HighDown and HighRight

╒ = HighDown and HighRight and HighDown_v1 and HighRight_v1
╓ = HighDown and HighRight and HighDown_v2 and HighRight_v2


=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background
Target
Obstacle
LowUp
LowDown
LowLeft
LowRight
Overlay
HighUp
HighDown
HighLeft
HighRight

LowUpDeco
LowDownDeco
LowLeftDeco
LowRightDeco
HighUpDeco
HighDownDeco
HighLeftDeco
HighRightDeco

CursorActive
Tweak

move56alttag

lmb
rmb
lmbup
rmbup
drag
Temp
move34lowertag

WinCon


======
RULES
======

(todo - replace certain cancels with feedback)

[ moving Player ] -> cancel

[ rmb Tweak ] -> [ ]
[ rmb Knot ] -> [ Tweak Knot ]

[ rmb ] -> [ ]

[ Tweak Overlay ] -> [ Overlay ]


[ lmb ] [ CursorActive ] -> [ lmb ] [ ]
[ lmb Knot ] -> [ CursorActiveStill Knot ]
[ lmb Crate ] -> [ CursorActiveStill Crate ]
[ lmb ] -> [ ]

[ drag Wall ] -> [ lmbup Wall ]

[ CursorActive | drag ] -> [ > CursorActiveMoved | ]

[ CursorActiveMoved ] [ move56alttag ] -> [ CursorActiveMoved ] [ ]

[ lmbup CursorActiveStill ] -> [ action CursorActiveStill ]

[ lmbup ] [ Tweak ] -> [ lmbup ] [ ]
[ lmbup ] [ CursorActive ] -> [ lmbup ] [ ]

[ lmbup ] -> [ ]

([ drag ] -> [ ])


(crate movement) ((((((((((((((((((((((((((((()))))))))))))))))))))))))))))

[ > CursorActive Crate | no Knot no Obstacle ] -> [ | CursorActive Crate ]
[ > CursorActive Crate ] -> [ CursorActive Crate ]
[ CursorActive Crate ] [ drag ] -> [ CursorActive Crate ] [ ]


(standard movement code) ((((((((((((((((((((((((((((()))))))))))))))))))))))))))))



[ vertical CursorActive Horizontal_ ] -> [ vertical CursorActive vertical Horizontal_ ] 
[ horizontal CursorActive Vertical_ ] -> [ horizontal CursorActive horizontal Vertical_ ]

up [ moving Up_ Tweak | no Tweak ] -> [ Up_ Tweak | ]
down [ moving Down_ Tweak | no Tweak ] -> [ Down_ Tweak | ]
left [ moving Left_ Tweak | no Tweak ] -> [ Left_ Tweak | ]
right [ moving Right_ Tweak | no Tweak ] -> [ Right_ Tweak | ]

up [ moving Up_ | Down_ ] -> [ moving Up_ | moving Down_ ]
+ [ moving Down_ Up_ no Tweak ] -> [ moving Down_ moving Up_ ]

down [ moving Down_ | Up_ ] -> [ moving Down_ | moving Up_ ]
+ [ moving Up_ Down_ no Tweak ] -> [ moving Up_ moving Down_ ]

left [ moving Left_ | Right_ ] -> [ moving Left_ | moving Right_ ]
+ [ moving Right_ Left_ no Tweak ] -> [ moving Right_ moving Left_ ]

right [ moving Right_ | Left_ ] -> [ moving Right_ | moving Left_ ]
+ [ moving Left_ Right_ no Tweak ] -> [ moving Left_ moving Right_ ]

[ > Up_ | Up_ ] -> cancel
[ > Down_ | Down_ ] -> cancel
[ > Left_ | Left_ ] -> cancel
[ > Right_ | Right_ ] -> cancel

up [ no Up_ > Knot | Knot ] -> cancel
down [ no Down_ > Knot | Knot ] -> cancel
left [ no Left_ > Knot | Knot ] -> cancel
right [ no Right_ > Knot | Knot ] -> cancel

[ > Knot | Obstacle ] -> cancel

[ moving Horizontal_ Vertical_ no Overlay ] -> [ moving Horizontal_ Vertical_ Temp ]
[ Horizontal_ moving Vertical_ no Overlay ] -> [ Horizontal_ moving Vertical_ Temp ]

[ moving Horizontal_ Tweak no Overlay ] -> [ moving Horizontal_ Tweak Temp ]
[ moving Vertical_ Tweak no Overlay ] -> [ moving Vertical_ Tweak Temp ]

up [ > Horizontal_ Temp Up_ | Down_ ] -> [ > Horizontal_ | ]
up [ > HighHorizontal Temp | ] -> [ > HighHorizontal HighUp | HighDown ]
up [ > LowHorizontal Temp | ] -> [ > LowHorizontal LowUp | LowDown ]

down [ > Horizontal_ Temp Down_ | Up_ ] -> [ > Horizontal_ | ]
down [ > HighHorizontal Temp | ] -> [ > HighHorizontal HighDown | HighUp ]
down [ > LowHorizontal Temp | ] -> [ > LowHorizontal LowDown | LowUp ]

left [ > Vertical_ Temp Left_ | Right_ ] -> [ > Vertical_ | ]
left [ > HighVertical Temp | ] -> [ > HighVertical HighLeft | HighRight ]
left [ > LowVertical Temp | ] -> [ > LowVertical LowLeft | LowRight ]

right [ > Vertical_ Temp Right_ | Left_ ] -> [ > Vertical_ | ]
right [ > HighVertical Temp | ] -> [ > HighVertical HighRight | HighLeft ]
right [ > LowVertical Temp | ] -> [ > LowVertical LowRight | LowLeft ]

[ Overlay > High | HighUp ] -> [ Overlay > High | LowUp ]
[ Overlay > High | HighDown ] -> [ Overlay > High | LowDown ]
[ Overlay > High | HighLeft ] -> [ Overlay > High | LowLeft ]
[ Overlay > High | HighRight ] -> [ Overlay > High | LowRight ]

[ Overlay > Low | LowUp ] -> [ Overlay > Low | HighUp ]
[ Overlay > Low | LowDown ] -> [ Overlay > Low | HighDown ]
[ Overlay > Low | LowLeft ] -> [ Overlay > Low | HighLeft ]
[ Overlay > Low | LowRight ] -> [ Overlay > Low | HighRight ]

[ > Knot | ] -> [ | Knot ]
[ > CursorActive | ] -> [ | CursorActive]

(moves 3 + 4) ((((((((((((((((((((((((((((()))))))))))))))))))))))))))))

[ CursorActive | | drag ] -> [ > CursorActiveMoved | | ]

[ vertical CursorActive Horizontal_ ] -> [ vertical CursorActive vertical Horizontal_ ] 
[ horizontal CursorActive Vertical_ ] -> [ horizontal CursorActive horizontal Vertical_ ]

up [ moving Up_ Tweak | no Tweak ] -> [ Up_ Tweak | ]
down [ moving Down_ Tweak | no Tweak ] -> [ Down_ Tweak | ]
left [ moving Left_ Tweak | no Tweak ] -> [ Left_ Tweak | ]
right [ moving Right_ Tweak | no Tweak ] -> [ Right_ Tweak | ]

up [ moving Up_ | Down_ ] -> [ moving Up_ | moving Down_ ]
+ [ moving Down_ Up_ no Tweak ] -> [ moving Down_ moving Up_ ]

down [ moving Down_ | Up_ ] -> [ moving Down_ | moving Up_ ]
+ [ moving Up_ Down_ no Tweak ] -> [ moving Up_ moving Down_ ]

left [ moving Left_ | Right_ ] -> [ moving Left_ | moving Right_ ]
+ [ moving Right_ Left_ no Tweak ] -> [ moving Right_ moving Left_ ]

right [ moving Right_ | Left_ ] -> [ moving Right_ | moving Left_ ]
+ [ moving Left_ Right_ no Tweak ] -> [ moving Left_ moving Right_ ]

[ moving Knot Overlay ] -> cancel

[ > Horizontal_ | Vertical_ no Overlay ] -> cancel
[ > Vertical_ | Horizontal_ no Overlay ] -> cancel

[ > Horizontal_ | LowVertical ] [ > Horizontal_ | HighVertical ] -> cancel
[ > Vertical_ | LowHorizontal ] [ > Vertical_ | HighHorizontal ] -> cancel

[ > Horizontal_ | LowVertical ] -> [ > Horizontal_ | LowVertical move34lowertag ]
[ > Vertical_ | LowHorizontal ] -> [ > Vertical_ | LowHorizontal move34lowertag ]

[ > Up_ | no Up_ ] -> cancel
[ > Down_ | no Down_ ] -> cancel
[ > Left_ | no Left_ ] -> cancel
[ > Right_ | no Right_ ] -> cancel

[ > Up_ | | Up_ ] -> cancel
[ > Down_ | | Down_ ] -> cancel
[ > Left_ | | Left_ ] -> cancel
[ > Right_ | | Right_ ] -> cancel

up [ no Up_ > Knot | | Knot ] -> cancel
down [ no Down_ > Knot | | Knot ] -> cancel
left [ no Left_ > Knot | | Knot ] -> cancel
right [ no Right_ > Knot | | Knot ] -> cancel

[ > Knot | Obstacle] -> cancel
[ > Knot | | Obstacle ] -> cancel

[ moving Horizontal_ Vertical_ no Overlay ] -> [ moving Horizontal_ Vertical_ Temp ]
[ Horizontal_ moving Vertical_ no Overlay ] -> [ Horizontal_ moving Vertical_ Temp ]

[ moving Horizontal_ Tweak no Overlay ] -> [ moving Horizontal_ Tweak Temp ]
[ moving Vertical_ Tweak no Overlay ] -> [ moving Vertical_ Tweak Temp ]

up [ > Horizontal_ Temp Up_ | Down_ Up_ | Down_ ] -> [ > Horizontal_ | | ]
up [ move34lowertag ] [ > Horizontal_ Temp | | ] -> [ move34lowertag ] [ > Horizontal_ HighUp | LowDown LowUp | HighDown ]
up [ > Horizontal_ Temp | | ] -> [ > Horizontal_ HighUp | HighDown HighUp LowLeft LowRight no HighLeft no HighRight | HighDown ]

down [ > Horizontal_ Temp Down_ | Up_ Down_ | Up_ ] -> [ > Horizontal_ | | ]
down [ move34lowertag ] [ > Horizontal_ Temp | | ] -> [ move34lowertag ] [ > Horizontal_ HighDown | LowUp LowDown | HighUp ]
down [ > Horizontal_ Temp | | ] -> [ > Horizontal_ HighDown | HighUp HighDown LowLeft LowRight no HighLeft no HighRight | HighUp ]

left [ > Vertical_ Temp Left_ | Right_ Left_ | Right_ ] -> [ > Vertical_ | | ]
left [ move34lowertag ] [ > Vertical_ Temp | | ] -> [ move34lowertag ] [ > Vertical_ HighLeft | LowRight LowLeft | HighRight ]
left [ > Vertical_ Temp | | ] -> [ > Vertical_ HighLeft | HighRight HighLeft LowUp LowDown no HighUp no HighDown | HighRight ]

right [ > Vertical_ Temp Right_ | Left_ Right_ | Left_ ] -> [ > Vertical_ | | ]
right [ move34lowertag ] [ > Vertical_ Temp | | ] -> [ move34lowertag ] [ > Vertical_ HighRight | LowLeft LowRight | HighLeft ]
right [ > Vertical_ Temp | | ] -> [ > Vertical_ HighRight | HighLeft HighRight LowUp LowDown no HighUp no HighDown | HighLeft ]

[ move34lowertag ] -> [ ]



[ > Knot | | ] -> [ | | Knot ]
[ > CursorActive | | ] -> [ | | CursorActive ]


( moves 5 + 6 ) (can't cancel in here ) (must remove CursorActive)

[ action CursorActive Overlay HighUp ] -> [ action CursorActive Overlay HighUp move56alttag ]

( 5 up )

up [ action CursorActive Overlay | HighDown no Overlay no HighUp ] -> [ action CursorActive Overlay | HighDown Temp ]
right [ HighDown Temp HighRight | HighLeft HighDown ] -> [ HighDown Temp HighRight | HighLeft HighDown Temp ]
left [ HighDown Temp HighLeft | HighRight HighDown ] -> [ HighDown Temp HighLeft | HighRight HighDown Temp]
down [ HighLeft HighDown Temp | HighUp HighLeft ] [ HighRight HighDown Temp ] [ CursorActive ] -> [ | ] [ ] [ HighLeft HighDown no Low no Up_ no Right_ ]
down [ HighRight HighDown Temp | HighUp HighRight ] [ HighLeft HighDown Temp ] [ CursorActive ] -> [ | ] [ ] [ HighRight HighDown no Low no Up_ no Left_ ]

[ Temp ] -> [ ]

( 5 down )
 
down [ action CursorActive Overlay | HighUp no Overlay no HighDown ] -> [ action CursorActive Overlay | HighUp Temp ]
right [ HighUp Temp HighRight | HighLeft HighUp ] -> [ HighUp Temp HighRight | HighLeft HighUp Temp ]
left [ HighUp Temp HighLeft | HighRight HighUp ] -> [ HighUp Temp HighLeft | HighRight HighUp Temp]
up [ HighLeft HighUp Temp | HighDown HighLeft ] [ HighRight HighUp Temp ] [ CursorActive ] -> [ | ] [ ] [ HighLeft HighUp no Low no Down_ no Right_ ]
up [ HighRight HighUp Temp | HighDown HighRight ] [ HighLeft HighUp Temp ] [ CursorActive ] -> [ | ] [ ] [ HighRight HighUp no Low no Down_ no Left_ ]

[ Temp ] -> [ ]

[ action CursorActive Overlay ] -> [ Overlay ]

[ HighUp HighDown action CursorActive ] -> [ HighUp HighDown ]
[ HighRight HighLeft action CursorActive ] -> [ HighRight HighLeft ]

[ HighUp HighLeft action CursorActive ] -> [ HighUp HighLeft action CursorActive Temp ]
down [ Temp action CursorActive | no Knot no Obstacle ] -> [ action Temp action CursorActive | action Temp ]
right [ action Temp | no Knot no Obstacle ] -> [ Temp | Temp ]
down [ Temp action CursorActive | Temp ] [ Temp no CursorActive | Temp ] -> [ Temp CursorActive | Temp ] [ Temp | Temp ]

[ action CursorActive ] [ Temp ] -> [ action CursorActive ] [ ]

[ HighUp HighRight action CursorActive ] -> [ HighUp HighRight action CursorActive Temp ]
down [ Temp action CursorActive | no Knot no Obstacle ] -> [ action Temp action CursorActive | action Temp ]
left [ action Temp | no Knot no Obstacle ] -> [ Temp | Temp ]
down [ Temp action CursorActive | Temp ] [ Temp no CursorActive | Temp ] -> [ Temp CursorActive | Temp ] [ Temp | Temp ]

[ action CursorActive ] [ Temp ] -> [ action CursorActive ] [ ]

[ HighDown HighLeft action CursorActive ] -> [ HighDown HighLeft action CursorActive Temp ]
up [ Temp action CursorActive | no Knot no Obstacle ] -> [ action Temp action CursorActive | action Temp ]
right [ action Temp | no Knot no Obstacle ] -> [ Temp | Temp ]
up [ Temp action CursorActive | Temp ] [ Temp no CursorActive | Temp ] -> [ Temp CursorActive | Temp ] [ Temp | Temp ]

[ action CursorActive ] [ Temp ] -> [ action CursorActive ] [ ]

[ HighDown HighRight action CursorActive ] -> [ HighDown HighRight action CursorActive Temp ]
up [ Temp action CursorActive | no Knot no Obstacle ] -> [ action Temp action CursorActive | action Temp ]
left [ action Temp | no Knot no Obstacle ] -> [ Temp | Temp ]
up [ Temp action CursorActive | Temp ] [ Temp no CursorActive | Temp ] -> [ Temp CursorActive | Temp ] [ Temp | Temp ]

[ action CursorActive ] [ Temp ] -> [ action CursorActive ] [ ]

right [ Temp | Temp ] -> [ Temp HighRight | HighLeft Temp ]
down [ Temp | Temp ] -> [ Temp HighDown | HighUp Temp ]

[ CursorActive Temp move56alttag ] -> [ LowUp LowDown no HighUp no HighDown ]
[ CursorActive Temp ] -> [ LowRight LowLeft no HighRight no HighLeft ]

[ Temp ] -> [ ]

[ action CursorActive ] -> [ ]




(((((((((((((((((((((((((((((((((())))))))))))))))))))))))))))))))))

late [ drag ] -> [ ]

late [ Tweak no Knot ] -> [ ]
late [ Tweak | Tweak CursorActive | Tweak ] -> [ Tweak | CursorActive | Tweak ] 

late [ Overlay ] -> [ ]
late [ High Low ] -> [ High Low Overlay ]

late [ LowUp no Overlay ] -> [ HighUp ]
late [ LowDown no Overlay ] -> [ HighDown ]
late [ LowLeft no Overlay ] -> [ HighLeft ]
late [ LowRight no Overlay ] -> [ HighRight ]

(win conditions)
late [ CrateTarget no Crate ] [ WinConCrates ] -> [ CrateTarget ] [ WinConCrates Temp ]

late [ CrossTarget no Overlay ] [ WinConCrosses ] -> [ CrossTarget ] [ WinConCrosses Temp ]
late [ Overlay no CrossTarget ] [ WinConCrosses ] -> [ Overlay ] [ WinConCrosses Temp ]

late [ WinConCrates no Temp ] [ WinCon Temp ] -> [ WinConCrates Temp ] [ WinCon Temp ]
late [ WinConCrosses no Temp ] [ WinCon Temp ] -> [ WinConCrosses Temp ] [ WinCon Temp ]
late [ WinCon no Temp ] -> [ WinCon ] win
late [ Temp ] -> [ ]

(update decorations)
late [ LowUpDeco no LowUp ] -> [ ]
late [ LowDownDeco no LowDown ] -> [ ]
late [ LowLeftDeco no LowLeft ] -> [ ]
late [ LowRightDeco no LowRight ] -> [ ]
late [ HighUpDeco no HighUp ] -> [ ]
late [ HighDownDeco no HighDown ] -> [ ]
late [ HighLeftDeco no HighLeft ] -> [ ]
late [ HighRightDeco no HighRight ] -> [ ]

late [ LowUp_v1 LowDown ] -> [ LowUp_v1 LowDown LowDown_v1 ]
+ late [ LowDown_v1 LowUp ] -> [ LowDown_v1 LowUp LowUp_v1 ]
+ late [ LowLeft_v1 LowRight ] -> [ LowLeft_v1 LowRight LowRight_v1 ]
+ late [ LowRight_v1 LowLeft ] -> [ LowRight_v1 LowLeft LowLeft_v1 ]
+ late [ HighUp_v1 HighDown ] -> [ HighUp_v1 HighDown HighDown_v1 ]
+ late [ HighUp_v1 HighLeft ] -> [ HighUp_v1 HighLeft HighLeft_v1 ]
+ late [ HighUp_v1 HighRight ] -> [ HighUp_v1 HighRight HighRight_v1 ]
+ late [ HighDown_v1 HighUp ] -> [ HighDown_v1 HighUp HighUp_v1 ]
+ late [ HighDown_v1 HighLeft ] -> [ HighDown_v1 HighLeft HighLeft_v1 ]
+ late [ HighDown_v1 HighRight ] -> [ HighDown_v1 HighRight HighRight_v1 ]
+ late [ HighLeft_v1 HighRight ] -> [ HighLeft_v1 HighRight HighRight_v1 ]
+ late [ HighLeft_v1 HighUp ] -> [ HighLeft_v1 HighUp HighUp_v1 ]
+ late [ HighLeft_v1 HighDown ] -> [ HighLeft_v1 HighDown HighDown_v1 ]
+ late [ HighRight_v1 HighLeft ] -> [ HighRight_v1 HighLeft HighLeft_v1 ]
+ late [ HighRight_v1 HighUp ] -> [ HighRight_v1 HighUp HighUp_v1 ]
+ late [ HighRight_v1 HighDown ] -> [ HighRight_v1 HighDown HighDown_v1 ]
+ late right [ Right_v1 | HighLeft ] -> [ Right_v1 | HighLeft HighLeft_v1 ]
+ late right [ Right_v1 | LowLeft ] -> [ Right_v1 | LowLeft LowLeft_v1 ]
+ late left [ Left_v1 | HighRight ] -> [ Left_v1 | HighRight HighRight_v1 ]
+ late left [ Left_v1 | LowRight ] -> [ Left_v1 | LowRight LowRight_v1 ]
+ late down [ Down_v1 | HighUp ] -> [ Down_v1 | HighUp HighUp_v1 ]
+ late down [ Down_v1 | LowUp ] -> [ Down_v1 | LowUp LowUp_v1 ]
+ late up [ Up_v1 | HighDown ] -> [ Up_v1 | HighDown HighDown_v1 ]
+ late up [ Up_v1 | LowDown ] -> [ Up_v1 | LowDown LowDown_v1 ]

late [ LowUp_v2 LowDown ] -> [ LowUp_v2 LowDown LowDown_v2 ]
+ late [ LowDown_v2 LowUp ] -> [ LowDown_v2 LowUp LowUp_v2 ]
+ late [ LowLeft_v2 LowRight ] -> [ LowLeft_v2 LowRight LowRight_v2 ]
+ late [ LowRight_v2 LowLeft ] -> [ LowRight_v2 LowLeft LowLeft_v2 ]
+ late [ HighUp_v2 HighDown ] -> [ HighUp_v2 HighDown HighDown_v2 ]
+ late [ HighUp_v2 HighLeft ] -> [ HighUp_v2 HighLeft HighLeft_v2 ]
+ late [ HighUp_v2 HighRight ] -> [ HighUp_v2 HighRight HighRight_v2 ]
+ late [ HighDown_v2 HighUp ] -> [ HighDown_v2 HighUp HighUp_v2 ]
+ late [ HighDown_v2 HighLeft ] -> [ HighDown_v2 HighLeft HighLeft_v2 ]
+ late [ HighDown_v2 HighRight ] -> [ HighDown_v2 HighRight HighRight_v2 ]
+ late [ HighLeft_v2 HighRight ] -> [ HighLeft_v2 HighRight HighRight_v2 ]
+ late [ HighLeft_v2 HighUp ] -> [ HighLeft_v2 HighUp HighUp_v2 ]
+ late [ HighLeft_v2 HighDown ] -> [ HighLeft_v2 HighDown HighDown_v2 ]
+ late [ HighRight_v2 HighLeft ] -> [ HighRight_v2 HighLeft HighLeft_v2 ]
+ late [ HighRight_v2 HighUp ] -> [ HighRight_v2 HighUp HighUp_v2 ]
+ late [ HighRight_v2 HighDown ] -> [ HighRight_v2 HighDown HighDown_v2 ]
+ late right [ Right_v2 | HighLeft ] -> [ Right_v2 | HighLeft HighLeft_v2 ]
+ late right [ Right_v2 | LowLeft ] -> [ Right_v2 | LowLeft LowLeft_v2 ]
+ late left [ Left_v2 | HighRight ] -> [ Left_v2 | HighRight HighRight_v2 ]
+ late left [ Left_v2 | LowRight ] -> [ Left_v2 | LowRight LowRight_v2 ]
+ late down [ Down_v2 | HighUp ] -> [ Down_v2 | HighUp HighUp_v2 ]
+ late down [ Down_v2 | LowUp ] -> [ Down_v2 | LowUp LowUp_v2 ]
+ late up [ Up_v2 | HighDown ] -> [ Up_v2 | HighDown HighDown_v2 ]
+ late up [ Up_v2 | LowDown ] -> [ Up_v2 | LowDown LowDown_v2 ]

(debug - should never happen, but might be hard to notice if they do)
(if these are messages weird stuff happens, don't make them messages )


late [ HighUp LowUp ] -> [ Temp ]
late [ HighDown LowDown ] -> [ Temp ]
late [ HighLeft LowLeft ] -> [ Temp ]
late [ HighRight LowRight ] -> [ Temp ]

late [ HighUp LowDown ] -> [ Temp ]
late [ HighDown LowUp ] -> [ Temp ]
late [ HighLeft LowRight ] -> [ Temp ]
late [ HighRight LowLeft ] -> [ Temp ]

late right [ Right_ | no Left_ ] -> [ Right_ | Temp ]
late left [ Left_ | no Right_ ] -> [ Left_ | Temp ]
late down [ Down_ | no Up_ ] -> [ Down_ | Temp ]
late up [ Up_ | no Down_ ] -> [ Up_ | Temp ]


==============
WINCONDITIONS
==============

=======
LEVELS
=======


###############
#.............#
#.............#
#.............#
#.............#
#.....┌┐......#
#....┌╂┿┐.....#
#....│└╂┘.....#
#....└─┘......#
#.............#
#.............#
#.............#
#.............#
#.............#
###############

###############
#.............#
#.............#
#...┌──┐......#
#...│.╓╂─┐....#
#...│╒╂┿┐│....#
#...└╂┿┘││....#
#....│└─┿┘....#
#....└──┘.....#
#.............#
#.............#
#.............#
#.............#
#.............#
###############

#############
#...........#
#...........#
#...........#
#....╒──┐...#
#...╓╂─┐│...#
#.+.││.││.+.#
#...│└─╂┘...#
#...└──┘....#
#...........#
#...........#
#...........#
############2

#########
#┌┐..####
#└┘..####
#....####
#.......#
####....#
####..+.#
####....#
########2

#########
###...###
###.*.###
#.┌───┐.#
#.│ooo│.#
#.└───┘.#
#.*.#.*.#
#...#...#
########1

###########
#.........#
#...+.+...#
#..╒───┐..#
#.+│╓─┐│+.#
#..││.││..#
#.+│└─┘│+.#
#..└───┘..#
#...+.+...#
#.........#
##########2

###############
#.............#
#.............#
#....┌──┐.....#
#....│┌─╂┐....#
#....││O││....#
#....│└┐││....#
#...┌┘┌┿┘│....#
#..┌╂─┿┿─┘....#
#..└╂─┘│......#
#...└──┘......#
#.............#
#.............#
#............*#
##############1
###############

###########
#####┌───┐#
#...#│.O.│#
#...#└┐.┌┘#
#┌─┐##│.│##
#│*│##│.│##
#│.└──┿─┘##
#│O...│..##
#│.┌──┘..##
#│*│#######
#└─┘#######
#...#######
#...#######
##########1

#.#
...
#.#
