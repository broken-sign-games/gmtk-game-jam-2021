title Knotical Nonsense
author MZCJ
homepage www.puzzlescript.net

youtube N3m2GBcDOIQ

verbose_logging

mouse_left
mouse_right
mouse_drag
mouse_up

run_rules_on_level_start

(noundo)

========
OBJECTS
========

lmb
pink

rmb
pink

lmbup
pink

rmbup
pink

drag
pink


Background
#9E6337

Plank1
#935b33
00000
0....
0....
0....
0....

Plank2
#935b33
00000
.....
.....
.....
.....

Plank3
#935b33
00000
.....
.....
.....
.....

Plank4
#935b33
00000
.....
.....
.....
.....

Plank5
#935b33
00000
.....
.....
.....
.....

Plank6
#935b33
00000
.....
.....
.....
.....

CursorActiveStill
red
00.00
0...0
.....
0...0
00.00

CursorActiveMoved
red
00.00
0...0
.....
0...0
00.00

CursorCrateAbove
red
.....
.....
.....
0...0
.....

CursorCrate
red
.....
.....
0...0
.....
0...0

CursorBarrelAbove
red
.....
.....
.....
.0.0.
0...0

CursorBarrel
red
.....
.....
.....
0...0
.0.0.

Tweak
black lightgray
.....
..0..
.010.
..0..
.....

Wall
#89E5FF

Wall_v1
#C9F2FF
.....
..0..
.0.0.
0...0
.....

Wall_v2
#C9F2FF
.....
..0..
.0.0.
.....
.....

Wall_v3
transparent

abovedecocrate
#51301B #7A4B2A #9E6337
.....
.....
.....
00000
02210

Crate
#51301B #7A4B2A #9E6337
02120
01220
00000
01110
00000

abovedecobarrel
#51301B #7A4B2A #9E6337
.....
.....
.....
.000.
01110

Barrel
#51301B #7A4B2A #9E6337
01210
01110
10001
02120
.000.

CrateTarget
#51301B
.....
.0.0.
..0..
.0.0.
.....

BarrelTarget
#51301B
.000.
0...0
0...0
0...0
.000.

CrossTarget
Purple
.0.0.
00.00
.....
00.00
.0.0.

HighUp
white
..0..
..0..
..0..
.....
.....

LowUp
white
..0..
.....
.....
.....
.....

HighDown
white
.....
.....
..0..
..0..
..0..


LowDown
white
.....
.....
.....
.....
..0..

HighLeft
white
.....
.....
000..
.....
.....

LowLeft
white
.....
.....
0....
.....
.....

HighRight
white
.....
.....
..000
.....
.....

LowRight
white
.....
.....
....0
.....
.....

HighUp_v1_0
#B99871 #CCBEA1
..0..
..1..
..0..
.....
.....

HighUp_v1_1
#CCBEA1 #B99871
..0..
..1..
..0..
.....
.....

LowUp_v1_0
#B99871 #CCBEA1
..0..
.....
.....
.....
.....

LowUp_v1_1
#CCBEA1 #B99871
..0..
.....
.....
.....
.....

HighDown_v1_0
#B99871 #CCBEA1
.....
.....
..0..
..1..
..0..

HighDown_v1_1
#CCBEA1 #B99871
.....
.....
..0..
..1..
..0..


LowDown_v1_0
#B99871 #CCBEA1
.....
.....
.....
.....
..0..

LowDown_v1_1
#CCBEA1 #B99871
.....
.....
.....
.....
..0..

HighLeft_v1_0
#B99871 #CCBEA1
.....
.....
010..
.....
.....

HighLeft_v1_1
#CCBEA1 #B99871
.....
.....
010..
.....
.....

LowLeft_v1_0
#B99871 #CCBEA1
.....
.....
0....
.....
.....

LowLeft_v1_1
#CCBEA1 #B99871
.....
.....
0....
.....
.....

HighRight_v1_0
#B99871 #CCBEA1
.....
.....
..010
.....
.....

HighRight_v1_1
#CCBEA1 #B99871
.....
.....
..010
.....
.....

LowRight_v1_0
#B99871 #CCBEA1
.....
.....
....0
.....
.....

LowRight_v1_1
#CCBEA1 #B99871
.....
.....
....0
.....
.....



HighUp_v2_0
red lightred
..0..
..1..
..0..
.....
.....

HighUp_v2_1
lightred red
..0..
..1..
..0..
.....
.....

LowUp_v2_0
red lightred
..0..
.....
.....
.....
.....

LowUp_v2_1
lightred red
..0..
.....
.....
.....
.....

HighDown_v2_0
red lightred
.....
.....
..0..
..1..
..0..

HighDown_v2_1
lightred red
.....
.....
..0..
..1..
..0..


LowDown_v2_0
red lightred
.....
.....
.....
.....
..0..

LowDown_v2_1
lightred red
.....
.....
.....
.....
..0..

HighLeft_v2_0
red lightred
.....
.....
010..
.....
.....

HighLeft_v2_1
lightred red
.....
.....
010..
.....
.....

LowLeft_v2_0
red lightred
.....
.....
0....
.....
.....

LowLeft_v2_1
lightred red
.....
.....
0....
.....
.....

HighRight_v2_0
red lightred
.....
.....
..010
.....
.....

HighRight_v2_1
lightred red
.....
.....
..010
.....
.....

LowRight_v2_0
red lightred
.....
.....
....0
.....
.....

LowRight_v2_1
lightred red
.....
.....
....0
.....
.....

Overlay (for putting on tiles with both high and low wires) (used to be visual, is now purely for logic)
transparent

WinConCrates
pink

WinConCrosses
pink

WinConRopes
pink

Temp
pink

lowertag
pink

move56alttag
transparent

loc11
pink

loc21
pink

loc31
pink

loc12
pink

loc22
pink

loc32
pink

loc13
pink

loc23
pink

loc33
pink



TargetCrossVertical
lightgreen
.0.0.
00.00
.0.0.
00.00
.0.0.

TargetCrossHorizontal
lightgreen
.0.0.
00000
.....
00000
.0.0.

TargetUpDown
lightgreen
.0.0.
.0.0.
.0.0.
.0.0.
.0.0.

TargetLeftRight
lightgreen
.....
00000
.....
00000
.....

TargetUpLeft
lightgreen
.0.0.
00.0.
...0.
0000.
.....

TargetUpRight
lightgreen
.0.0.
.0.00
.0...
.0000
.....

TargetDownLeft
lightgreen
.....
0000.
...0.
00.0.
.0.0.

TargetDownRight
lightgreen
.....
.0000
.0...
.0.00
.0.0.

MovableAnchor
yellow
.0.0.
0...0
.....
0...0
.0.0.

StaticAnchor
darkred
.0.0.
0...0
.....
0...0
.0.0.

HighlightSquare
red
.....
.000.
.000.
.000.
.....


=======
LEGEND
=======

. = Background
# = Wall
* = CrateTarget
_ = BarrelTarget
+ = CrossTarget
O = Crate
U = Barrel

i = MovableAnchor and HighUp
k = MovableAnchor and HighDown
j = MovableAnchor and HighLeft
l = MovableAnchor and HighRight

1 = StaticAnchor and HighUp
2 = StaticAnchor and HighDown
3 = StaticAnchor and HighLeft
4 = StaticAnchor and HighRight

Player = Background (necessary for language reasons)

WinCon = WinConCrates or WinConCrosses or WinConRopes
Target = CrossTarget or CrateTarget or BarrelTarget or TargetCrossVertical or TargetCrossHorizontal or TargetUpDown or TargetLeftRight or TargetUpLeft or TargetUpRight or TargetDownLeft or TargetDownRight
Cratelike = Crate or Barrel
Anchor = MovableAnchor or StaticAnchor
Obstacle = Wall or Cratelike or Anchor



CursorActive = CursorActiveStill or CursorActiveMoved or CursorCrate or CursorBarrel

High = HighUp or HighDown or HighLeft or HighRight
Low = LowUp or LowDown or LowLeft or LowRight

Knot = High or Low

Up_ = HighUp or LowUp
Down_ = HighDown or LowDown
Left_ = HighLeft or LowLeft
Right_ = HighRight or LowRight

Horizontal_ = Left_ or Right_
Vertical_ = Up_ or Down_

HighHorizontal = HighLeft or HighRight
HighVertical = HighUp or HighDown

LowHorizontal = LowLeft or LowRight
LowVertical = LowUp or LowDown

HighLeft_v1 = HighLeft_v1_0 or HighLeft_v1_1
HighRight_v1 = HighRight_v1_0 or HighRight_v1_1
HighUp_v1 = HighUp_v1_0 or HighUp_v1_1
HighDown_v1 = HighDown_v1_0 or HighDown_v1_1
LowLeft_v1 = LowLeft_v1_0 or LowLeft_v1_1
LowRight_v1 = LowRight_v1_0 or LowRight_v1_1
LowUp_v1 = LowUp_v1_0 or LowUp_v1_1
LowDown_v1 = LowDown_v1_0 or LowDown_v1_1

Left_v1_0 = LowLeft_v1_0 or HighLeft_v1_0
Left_v1_1 = LowLeft_v1_1 or HighLeft_v1_1
Right_v1_0 = LowRight_v1_0 or HighRight_v1_0
Right_v1_1 = LowRight_v1_1 or HighRight_v1_1
Up_v1_0 = LowUp_v1_0 or HighUp_v1_0
Up_v1_1 = LowUp_v1_1 or HighUp_v1_1
Down_v1_0 = LowDown_v1_0 or HighDown_v1_0
Down_v1_1 = LowDown_v1_1 or HighDown_v1_1

Low_v1 = LowUp_v1 or LowDown_v1 or LowLeft_v1 or LowRight_v1
High_v1 = HighUp_v1 or HighDown_v1 or HighLeft_v1 or HighRight_v1

HighLeft_v2 = HighLeft_v2_0 or HighLeft_v2_1
HighRight_v2 = HighRight_v2_0 or HighRight_v2_1
HighUp_v2 = HighUp_v2_0 or HighUp_v2_1
HighDown_v2 = HighDown_v2_0 or HighDown_v2_1
LowLeft_v2 = LowLeft_v2_0 or LowLeft_v2_1
LowRight_v2 = LowRight_v2_0 or LowRight_v2_1
LowUp_v2 = LowUp_v2_0 or LowUp_v2_1
LowDown_v2 = LowDown_v2_0 or LowDown_v2_1

Left_v2_0 = LowLeft_v2_0 or HighLeft_v2_0
Left_v2_1 = LowLeft_v2_1 or HighLeft_v2_1
Right_v2_0 = LowRight_v2_0 or HighRight_v2_0
Right_v2_1 = LowRight_v2_1 or HighRight_v2_1
Up_v2_0 = LowUp_v2_0 or HighUp_v2_0
Up_v2_1 = LowUp_v2_1 or HighUp_v2_1
Down_v2_0 = LowDown_v2_0 or HighDown_v2_0
Down_v2_1 = LowDown_v2_1 or HighDown_v2_1

Low_v2 = LowUp_v2 or LowDown_v2 or LowLeft_v2 or LowRight_v2
High_v2 = HighUp_v2 or HighDown_v2 or HighLeft_v2 or HighRight_v2

HighLeftDeco = HighLeft_v1 or HighLeft_v2
HighRightDeco = HighRight_v1 or HighRight_v2
HighUpDeco = HighUp_v1 or HighUp_v2
HighDownDeco = HighDown_v1 or HighDown_v2
LowLeftDeco = LowLeft_v1 or LowLeft_v2
LowRightDeco = LowRight_v1 or LowRight_v2
LowUpDeco = LowUp_v1 or LowUp_v2
LowDownDeco = LowDown_v1 or LowDown_v2

LowDeco = Low_v1 or Low_v2
HighDeco = High_v1 or High_v2
Decoration = LowDeco or HighDeco

WallDeco = Wall_v1 or Wall_v2 or Wall_v3


╂ = HighUp and HighDown and Overlay and LowLeft and LowRight
┿ = HighLeft and HighRight and Overlay and LowUp and LowDown

│ = HighUp and HighDown
─ = HighLeft and HighRight
┘ = HighUp and HighLeft
└ = HighUp and HighRight
┐ = HighDown and HighLeft
┌ = HighDown and HighRight

╒ = HighDown and HighRight and HighDown_v1_0 and HighRight_v1_0
╓ = HighDown and HighRight and HighDown_v2_0 and HighRight_v2_0


╫ = TargetCrossVertical
╪ = TargetCrossHorizontal

║ = TargetUpDown
═ = TargetLeftRight
╝ = TargetUpLeft
╚ = TargetUpRight
╗ = TargetDownLeft
╔ = TargetDownRight

loc = loc11 or loc21 or loc31 or loc12 or loc22 or loc32 or loc13 or loc23 or loc33

KnotA = Up_ or Left_
KnotB = Down_ or Right_

AboveDeco = abovedecocrate or abovedecobarrel
AboveCursor = CursorCrateAbove or CursorBarrelAbove
(BelowDeco = )

Isometric = AboveDeco or AboveCursor (or BelowDeco)

Plank = Plank1 or Plank2 or Plank3 or Plank4 or Plank5 or Plank6

P3R = Plank4 or Plank1

P4R = Plank5 or Plank1

P5R = Plank6 or Plank1


=======
SOUNDS
=======

(sfx:
0 = crate/barrel move
1 = standard rope movement
2 = moving a rope over/under
3 = twisting/untwisting a corner
4 = flip moves
)


================
COLLISIONLAYERS
================

Background

Plank


Target

(BelowDeco)



LowUp
LowDown
LowLeft
LowRight
Overlay
HighUp
HighDown
HighLeft
HighRight

Obstacle
WallDeco

LowUpDeco
LowDownDeco
LowLeftDeco
LowRightDeco
HighUpDeco
HighDownDeco
HighLeftDeco
HighRightDeco

HighlightSquare
CursorActive
Tweak

AboveDeco
AboveCursor

move56alttag

lmb
rmb
lmbup
rmbup
drag
Temp
lowertag
loc

WinCon


======
RULES
======

random [ no Plank ] -> [ Temp ]

[ ] [ Temp ] -> [ random Plank ] [ ]

down [ Plank | no Plank ] -> [ Plank | random Plank ]

right [ Plank1 | no Plank ] -> [ Plank1 | Plank2 ]
+ right [ Plank2 | no Plank ] -> [ Plank2 | Plank3 ]
+ right [ Plank3 | no Plank ] -> [ Plank3 | random P3R ]
+ right [ Plank4 | no Plank ] -> [ Plank4 | random P4R ]
+ right [ Plank5 | no Plank ] -> [ Plank5 | random P5R ]
+ right [ Plank6 | no Plank ] -> [ Plank6 | Plank1 ]

(todo - replace certain cancels with feedback)

[ moving Player ] -> cancel

[ HighlightSquare ] -> [ ]

[ rmb Tweak ] -> [ ]
[ rmb Knot ] -> [ Tweak Knot ]

[ rmb ] -> [ ]

[ Tweak Overlay ] -> [ Overlay ]


[ lmb ] [ CursorActive ] -> [ lmb ] [ ]
[ lmb Knot ] -> [ CursorActiveStill Knot ]
[ lmb Crate ] -> [ CursorActiveStill Crate ]
[ lmb Barrel ] -> [ CursorActiveStill Barrel ]
[ lmb ] -> [ ]

[ drag Wall ] -> [ lmbup Wall ]

[ CursorActive | drag ] -> [ > CursorActiveMoved | ]

[ CursorActiveMoved ] [ move56alttag ] -> [ CursorActiveMoved ] [ ]

[ lmbup CursorActiveStill ] -> [ action CursorActiveStill ]

[ lmbup ] [ Tweak ] -> [ lmbup ] [ ]
[ lmbup ] [ CursorActive ] -> [ lmbup ] [ ]

[ lmbup ] -> [ ]

([ drag ] -> [ ])


(crate and barrel movement) ((((((((((((((((((((((((((((()))))))))))))))))))))))))))))

[ > CursorActive Cratelike | no Knot no Obstacle ] -> [ | CursorActive Cratelike ] sfx0 (crate/barrel)
[ > CursorActive Cratelike ] -> [ CursorActive Cratelike ]
[ CursorActive Cratelike ] [ drag ] -> [ CursorActive Cratelike ] [ ]

(movable anchor movement )

[ > CursorActive MovableAnchor | no Knot no Obstacle ] -> [ > MovableAnchor | CursorActive ]

up [ > MovableAnchor | ] -> [ HighUp | HighDown MovableAnchor ]
down [ > MovableAnchor | ] -> [ HighDown | HighUp MovableAnchor ]
left [ > MovableAnchor | ] -> [ HighLeft | HighRight MovableAnchor ]
right [ > MovableAnchor | ] -> [ HighRight | HighLeft MovableAnchor ]


[ > CursorActive MovableAnchor | no Obstacle no Overlay ] -> [ > MovableAnchor | CursorActive ]

[ up MovableAnchor no HighUp ]  -> cancel
[ down MovableAnchor no HighDown ] -> cancel
[ left MovableAnchor no HighLeft ] -> cancel
[ right MovableAnchor no HighRight ] -> cancel

[ moving MovableAnchor ] -> [ moving MovableAnchor no Knot ]

up [ > MovableAnchor | ] -> [ | no Down_ MovableAnchor ]
down [ > MovableAnchor | ] -> [ | no Up_ MovableAnchor ]
left [ > MovableAnchor | ] -> [ | no Right_ MovableAnchor ]
right [ > MovableAnchor | ] -> [ | no Left_ MovableAnchor ]


[ MovableAnchor HighUp ] -> [ MovableAnchor HighUp ]
[ MovableAnchor HighDown ] -> [ MovableAnchor HighDown ]
[ MovableAnchor HighLeft ] -> [ MovableAnchor HighLeft ]
[ MovableAnchor HighRight ] -> [ MovableAnchor HighRight ]



(standard movement code) ((((((((((((((((((((((((((((()))))))))))))))))))))))))))))

[ vertical CursorActive Horizontal_ ] -> [ vertical CursorActive vertical Horizontal_ ] 
[ horizontal CursorActive Vertical_ ] -> [ horizontal CursorActive horizontal Vertical_ ]

up [ moving Up_ Tweak | no Tweak ] -> [ Up_ Tweak | ]
down [ moving Down_ Tweak | no Tweak ] -> [ Down_ Tweak | ]
left [ moving Left_ Tweak | no Tweak ] -> [ Left_ Tweak | ]
right [ moving Right_ Tweak | no Tweak ] -> [ Right_ Tweak | ]

up [ moving Up_ | Down_ ] -> [ moving Up_ | moving Down_ ]
+ [ moving Down_ Up_ no Tweak ] -> [ moving Down_ moving Up_ ]

down [ moving Down_ | Up_ ] -> [ moving Down_ | moving Up_ ]
+ [ moving Up_ Down_ no Tweak ] -> [ moving Up_ moving Down_ ]

left [ moving Left_ | Right_ ] -> [ moving Left_ | moving Right_ ]
+ [ moving Right_ Left_ no Tweak ] -> [ moving Right_ moving Left_ ]

right [ moving Right_ | Left_ ] -> [ moving Right_ | moving Left_ ]
+ [ moving Left_ Right_ no Tweak ] -> [ moving Left_ moving Right_ ]

[ > Up_ | Up_ ] -> cancel
[ > Down_ | Down_ ] -> cancel
[ > Left_ | Left_ ] -> cancel
[ > Right_ | Right_ ] -> cancel

up [ no Up_ > Knot | Knot ] -> cancel
down [ no Down_ > Knot | Knot ] -> cancel
left [ no Left_ > Knot | Knot ] -> cancel
right [ no Right_ > Knot | Knot ] -> cancel

up [ > Knot | Obstacle no HighDown ] -> cancel
down [ > Knot | Obstacle no HighUp ] -> cancel
left [ > Knot | Obstacle no HighRight ] -> cancel
right [ > Knot | Obstacle no HighLeft ] -> cancel

[ moving Horizontal_ Vertical_ no Overlay ] -> [ moving Horizontal_ Vertical_ Temp ]
[ Horizontal_ moving Vertical_ no Overlay ] -> [ Horizontal_ moving Vertical_ Temp ]

[ moving Horizontal_ Tweak no Overlay ] -> [ moving Horizontal_ Tweak Temp ]
[ moving Vertical_ Tweak no Overlay ] -> [ moving Vertical_ Tweak Temp ]

[ moving Horizontal_ Anchor no Overlay ] -> [ moving Horizontal_ Anchor Temp ]
[ moving Vertical_ Anchor no Overlay ] -> [ moving Vertical_ Anchor Temp ]

up [ > Horizontal_ Temp Up_ | Down_ ] -> [ > Horizontal_ | ]
up [ > HighHorizontal Temp | ] -> [ > HighHorizontal HighUp | HighDown ]
up [ > LowHorizontal Temp | ] -> [ > LowHorizontal LowUp | LowDown ]

down [ > Horizontal_ Temp Down_ | Up_ ] -> [ > Horizontal_ | ]
down [ > HighHorizontal Temp | ] -> [ > HighHorizontal HighDown | HighUp ]
down [ > LowHorizontal Temp | ] -> [ > LowHorizontal LowDown | LowUp ]

left [ > Vertical_ Temp Left_ | Right_ ] -> [ > Vertical_ | ]
left [ > HighVertical Temp | ] -> [ > HighVertical HighLeft | HighRight ]
left [ > LowVertical Temp | ] -> [ > LowVertical LowLeft | LowRight ]

right [ > Vertical_ Temp Right_ | Left_ ] -> [ > Vertical_ | ]
right [ > HighVertical Temp | ] -> [ > HighVertical HighRight | HighLeft ]
right [ > LowVertical Temp | ] -> [ > LowVertical LowRight | LowLeft ]

[ Overlay > High | HighUp ] -> [ Overlay > High | LowUp ]
[ Overlay > High | HighDown ] -> [ Overlay > High | LowDown ]
[ Overlay > High | HighLeft ] -> [ Overlay > High | LowLeft ]
[ Overlay > High | HighRight ] -> [ Overlay > High | LowRight ]

[ Overlay > Low | LowUp ] -> [ Overlay > Low | HighUp ]
[ Overlay > Low | LowDown ] -> [ Overlay > Low | HighDown ]
[ Overlay > Low | LowLeft ] -> [ Overlay > Low | HighLeft ]
[ Overlay > Low | LowRight ] -> [ Overlay > Low | HighRight ]

[ > Knot | ] -> [ | Knot ]
[ > CursorActive | ] -> [ | CursorActive ] sfx1

[ CursorActive High Low ] -> [ CursorActive High Low Overlay ]

(moves 3 + 4) ((((((((((((((((((((((((((((()))))))))))))))))))))))))))))

[ CursorActive | | drag ] -> [ > CursorActiveMoved | | ]

[ vertical CursorActive Horizontal_ ] -> [ vertical CursorActive vertical Horizontal_ ] 
[ horizontal CursorActive Vertical_ ] -> [ horizontal CursorActive horizontal Vertical_ ]

up [ moving Up_ Tweak | no Tweak ] -> [ Up_ Tweak | ]
down [ moving Down_ Tweak | no Tweak ] -> [ Down_ Tweak | ]
left [ moving Left_ Tweak | no Tweak ] -> [ Left_ Tweak | ]
right [ moving Right_ Tweak | no Tweak ] -> [ Right_ Tweak | ]

up [ moving Up_ | Down_ ] -> [ moving Up_ | moving Down_ ]
+ [ moving Down_ Up_ no Tweak ] -> [ moving Down_ moving Up_ ]

down [ moving Down_ | Up_ ] -> [ moving Down_ | moving Up_ ]
+ [ moving Up_ Down_ no Tweak ] -> [ moving Up_ moving Down_ ]

left [ moving Left_ | Right_ ] -> [ moving Left_ | moving Right_ ]
+ [ moving Right_ Left_ no Tweak ] -> [ moving Right_ moving Left_ ]

right [ moving Right_ | Left_ ] -> [ moving Right_ | moving Left_ ]
+ [ moving Left_ Right_ no Tweak ] -> [ moving Left_ moving Right_ ]

[ moving Knot Overlay ] -> cancel

[ > Horizontal_ | Vertical_ no Overlay ] -> cancel
[ > Vertical_ | Horizontal_ no Overlay ] -> cancel

[ > Horizontal_ | LowVertical ] [ > Horizontal_ | HighVertical ] -> cancel
[ > Vertical_ | LowHorizontal ] [ > Vertical_ | HighHorizontal ] -> cancel

[ > Horizontal_ | LowVertical ] -> [ > Horizontal_ | LowVertical lowertag ]
[ > Vertical_ | LowHorizontal ] -> [ > Vertical_ | LowHorizontal lowertag ]

[ > Up_ | no Up_ ] -> cancel
[ > Down_ | no Down_ ] -> cancel
[ > Left_ | no Left_ ] -> cancel
[ > Right_ | no Right_ ] -> cancel

[ > Up_ | | Up_ ] -> cancel
[ > Down_ | | Down_ ] -> cancel
[ > Left_ | | Left_ ] -> cancel
[ > Right_ | | Right_ ] -> cancel

up [ no Up_ > Knot | | Knot ] -> cancel
down [ no Down_ > Knot | | Knot ] -> cancel
left [ no Left_ > Knot | | Knot ] -> cancel
right [ no Right_ > Knot | | Knot ] -> cancel

[ > Knot | Obstacle] -> cancel

up [ > Knot | | Obstacle no HighDown ] -> cancel
down [ > Knot | | Obstacle no HighUp ] -> cancel
left [ > Knot | | Obstacle no HighRight ] -> cancel
right [ > Knot | | Obstacle no HighLeft ] -> cancel

[ moving Horizontal_ Vertical_ no Overlay ] -> [ moving Horizontal_ Vertical_ Temp ]
[ Horizontal_ moving Vertical_ no Overlay ] -> [ Horizontal_ moving Vertical_ Temp ]

[ moving Horizontal_ Tweak no Overlay ] -> [ moving Horizontal_ Tweak Temp ]
[ moving Vertical_ Tweak no Overlay ] -> [ moving Vertical_ Tweak Temp ]

[ moving Horizontal_ Anchor no Overlay ] -> [ moving Horizontal_ Anchor Temp ]
[ moving Vertical_ Anchor no Overlay ] -> [ moving Vertical_ Anchor Temp ]

up [ > Horizontal_ Temp Up_ | Down_ Up_ | Down_ ] -> [ > Horizontal_ | | ]
up [ lowertag ] [ > Horizontal_ Temp | | ] -> [ lowertag ] [ > Horizontal_ HighUp | LowDown LowUp | HighDown ]
up [ > Horizontal_ Temp | | ] -> [ > Horizontal_ HighUp | HighDown HighUp LowLeft LowRight no HighLeft no HighRight | HighDown ]

down [ > Horizontal_ Temp Down_ | Up_ Down_ | Up_ ] -> [ > Horizontal_ | | ]
down [ lowertag ] [ > Horizontal_ Temp | | ] -> [ lowertag ] [ > Horizontal_ HighDown | LowUp LowDown | HighUp ]
down [ > Horizontal_ Temp | | ] -> [ > Horizontal_ HighDown | HighUp HighDown LowLeft LowRight no HighLeft no HighRight | HighUp ]

left [ > Vertical_ Temp Left_ | Right_ Left_ | Right_ ] -> [ > Vertical_ | | ]
left [ lowertag ] [ > Vertical_ Temp | | ] -> [ lowertag ] [ > Vertical_ HighLeft | LowRight LowLeft | HighRight ]
left [ > Vertical_ Temp | | ] -> [ > Vertical_ HighLeft | HighRight HighLeft LowUp LowDown no HighUp no HighDown | HighRight ]

right [ > Vertical_ Temp Right_ | Left_ Right_ | Left_ ] -> [ > Vertical_ | | ]
right [ lowertag ] [ > Vertical_ Temp | | ] -> [ lowertag ] [ > Vertical_ HighRight | LowLeft LowRight | HighLeft ]
right [ > Vertical_ Temp | | ] -> [ > Vertical_ HighRight | HighLeft HighRight LowUp LowDown no HighUp no HighDown | HighLeft ]

[ lowertag ] -> [ ]



[ > Knot | | ] -> [ | | Knot ]
[ > CursorActive | | ] -> [ | | CursorActive Overlay ] sfx2

[ CursorActive High Low ] -> [ CursorActive High Low Overlay ]

( moves 5 + 6 ) (can't cancel in here ) (must remove CursorActive)
( TODO: maybe highlight crossings that permit a 5 move? )

[ action CursorActive Overlay HighUp ] -> [ action CursorActive Overlay HighUp move56alttag ]

( 5 up )

up [ action CursorActive Overlay | HighDown no Overlay no HighUp ] -> [ action CursorActive Overlay | HighDown Temp ]
right [ HighDown Temp HighRight | HighLeft HighDown ] -> [ HighDown Temp HighRight | HighLeft HighDown Temp ]
left [ HighDown Temp HighLeft | HighRight HighDown ] -> [ HighDown Temp HighLeft | HighRight HighDown Temp]
down [ HighLeft HighDown Temp | HighUp HighLeft ] [ HighRight HighDown Temp ] [ CursorActive ] -> [ | ] [ ] [ HighLeft HighDown no Low no Up_ no Right_ ] sfx3
down [ HighRight HighDown Temp | HighUp HighRight ] [ HighLeft HighDown Temp ] [ CursorActive ] -> [ | ] [ ] [ HighRight HighDown no Low no Up_ no Left_ ] sfx3

[ Temp ] -> [ ]

( 5 down )
 
down [ action CursorActive Overlay | HighUp no Overlay no HighDown ] -> [ action CursorActive Overlay | HighUp Temp ]
right [ HighUp Temp HighRight | HighLeft HighUp ] -> [ HighUp Temp HighRight | HighLeft HighUp Temp ]
left [ HighUp Temp HighLeft | HighRight HighUp ] -> [ HighUp Temp HighLeft | HighRight HighUp Temp]
up [ HighLeft HighUp Temp | HighDown HighLeft ] [ HighRight HighUp Temp ] [ CursorActive ] -> [ | ] [ ] [ HighLeft HighUp no Low no Down_ no Right_ ] sfx3
up [ HighRight HighUp Temp | HighDown HighRight ] [ HighLeft HighUp Temp ] [ CursorActive ] -> [ | ] [ ] [ HighRight HighUp no Low no Down_ no Left_ ] sfx3

[ Temp ] -> [ ]

[ action CursorActive Overlay ] -> [ Overlay ]

[ HighUp HighDown action CursorActive ] -> [ HighUp HighDown ]
[ HighRight HighLeft action CursorActive ] -> [ HighRight HighLeft ]

[ HighUp HighLeft action CursorActive ] -> [ HighUp HighLeft action CursorActive Temp ]
down [ Temp action CursorActive | no Knot no Obstacle ] -> [ action Temp action CursorActive | action Temp ]
right [ action Temp | no Knot no Obstacle ] -> [ Temp | Temp ]
down [ Temp action CursorActive | Temp ] [ Temp no CursorActive | Temp ] -> [ Temp CursorActive | Temp ] [ Temp | Temp ]

[ action CursorActive ] [ Temp ] -> [ action CursorActive ] [ ]

[ HighUp HighRight action CursorActive ] -> [ HighUp HighRight action CursorActive Temp ]
down [ Temp action CursorActive | no Knot no Obstacle ] -> [ action Temp action CursorActive | action Temp ]
left [ action Temp | no Knot no Obstacle ] -> [ Temp | Temp ]
down [ Temp action CursorActive | Temp ] [ Temp no CursorActive | Temp ] -> [ Temp CursorActive | Temp ] [ Temp | Temp ]

[ action CursorActive ] [ Temp ] -> [ action CursorActive ] [ ]

[ HighDown HighLeft action CursorActive ] -> [ HighDown HighLeft action CursorActive Temp ]
up [ Temp action CursorActive | no Knot no Obstacle ] -> [ action Temp action CursorActive | action Temp ]
right [ action Temp | no Knot no Obstacle ] -> [ Temp | Temp ]
up [ Temp action CursorActive | Temp ] [ Temp no CursorActive | Temp ] -> [ Temp CursorActive | Temp ] [ Temp | Temp ]

[ action CursorActive ] [ Temp ] -> [ action CursorActive ] [ ]

[ HighDown HighRight action CursorActive ] -> [ HighDown HighRight action CursorActive Temp ]
up [ Temp action CursorActive | no Knot no Obstacle ] -> [ action Temp action CursorActive | action Temp ]
left [ action Temp | no Knot no Obstacle ] -> [ Temp | Temp ]
up [ Temp action CursorActive | Temp ] [ Temp no CursorActive | Temp ] -> [ Temp CursorActive | Temp ] [ Temp | Temp ]

[ action CursorActive ] [ Temp ] -> [ action CursorActive ] [ ]

right [ Temp | Temp ] -> [ Temp HighRight | HighLeft Temp ]
down [ Temp | Temp ] -> [ Temp HighDown | HighUp Temp ]

[ CursorActive Temp move56alttag ] -> [ LowUp LowDown no HighUp no HighDown ] sfx3
[ CursorActive Temp ] -> [ LowRight LowLeft no HighRight no HighLeft ] sfx3

[ Temp ] -> [ ]

[ action CursorActive ] -> [ ]

(rest of the moves) (((((((((((((((((((((((((((((((((())))))))))))))))))))))))))))))))))
(move highlighting) (((((((((((((((((((((((((((((((((())))))))))))))))))))))))))))))))))

[ CursorActive Vertical_ Horizontal_ no Overlay ] -> [ action CursorActive Vertical_ Horizontal_ ]

up [ action CursorActive HighUp | | ] -> [ action CursorActive HighUp loc11 | loc12 | loc13 ]
down [ action CursorActive HighDown | | ] -> [ action CursorActive HighDown loc11 | loc12 | loc13 ]

left [ action CursorActive HighLeft ] [ loc | | ] -> [ action CursorActive HighLeft ] [ loc | | Temp ]
right [ action CursorActive HighRight ] [ loc | | ] -> [ action CursorActive HighRight ] [ loc | | Temp ]


[ loc11 | | Temp ] -> [ loc11 | loc21 | loc31 ]
[ loc12 | | Temp ] -> [ loc12 | loc22 | loc32 ]
[ loc13 | | Temp ] -> [ loc13 | loc23 | loc33 ]

[ loc Up_ ] -> [ loc up Up_ ]
[ loc Down_ ] -> [ loc down Down_ ]
[ loc Left_ ] -> [ loc left Left_ ]
[ loc Right_ ] -> [ loc right Right_ ]

[ loc11 | loc12 parallel Low ] -> [ lowertag loc11 | loc12 parallel low ]
[ loc11 | loc21 parallel Low ] -> [ lowertag loc11 | loc21 parallel low ]

[ lowertag loc11 | loc12 perpendicular Low ] -> [ lowertag loc11 | loc12 perpendicular Low Temp ]
[ lowertag loc11 | loc21 perpendicular Low ] -> [ lowertag loc11 | loc21 perpendicular Low Temp ]

[ loc33 Knot ] -> [ loc33 Knot Temp ] 
[ loc22 no Knot ] -> [ loc22 Temp ] (disallow simple corner inversions, could be allowed probably)

[ loc13 | loc23 parallel Knot ] -> [ loc13 | loc23 parallel Knot Temp ]
[ loc31 | loc32 parallel Knot ] -> [ loc31 | loc32 parallel Knot Temp ]

[ loc Obstacle ] -> [ Loc Obstacle Temp ]

[ Temp ] [ action CursorActive ] -> [ ] [ CursorActive ]

[ loc11 > KnotB | < KnotA loc12 > KnotB | < KnotA loc13 ] -> [ Temp loc11 > KnotB | < KnotA loc12 > KnotB | < KnotA loc13 ]
[ loc11 > KnotA | < KnotB loc12 > KnotA | < KnotB loc13 ] -> [ Temp loc11 > KnotA | < KnotB loc12 > KnotA | < KnotB loc13 ]
[ action CursorActive no Temp ] -> [ CursorActive ]
[ Temp ] -> [ ]

[ loc11 > KnotB | < KnotA loc21 > KnotB | < KnotA loc31 ] -> [ Temp loc11 > KnotB | < KnotA loc21 > KnotB | < KnotA loc31 ]
[ loc11 > KnotA | < KnotB loc21 > KnotA | < KnotB loc31 ] -> [ Temp loc11 > KnotA | < KnotB loc21 > KnotA | < KnotB loc31 ]
[ action CursorActive no Temp ] -> [ CursorActive ]
[ Temp ] -> [ ]

[ action CursorActive ] [ loc33 ] -> [ CursorActive ] [ loc33 Highlightsquare ]

[ HighlightSquare no drag ] [ loc ] -> [ HighlightSquare ] [ ]

[ loc33 no HighlightSquare ] -> [ Temp ]

[ Temp ] [ loc ] -> [ Temp ] [ ]

[ Temp ] -> [ ]

[ CursorActive ] [ drag HighlightSquare ] -> [ HighlightSquare ] [ CursorActiveMoved ] sfx4

[ loc11 > KnotB | < KnotA loc12 > KnotB | < KnotA loc13 ] -> [ loc11 | loc12 | loc13 ]
[ loc11 > KnotA | < KnotB loc12 > KnotA | < KnotB loc13 ] -> [ loc11 | loc12 | loc13 ]
[ loc11 > KnotB | < KnotA loc21 > KnotB | < KnotA loc31 ] -> [ loc11 | loc21 | loc31 ]
[ loc11 > KnotA | < KnotB loc21 > KnotA | < KnotB loc31 ] -> [ loc11 | loc21 | loc31 ]

[ loc13 ] -> [ action loc13 ]
[ loc23 ] -> [ action loc23 ]
[ loc33 ] -> [ action loc33 ]
[ loc32 ] -> [ action loc32 ]
[ loc31 ] -> [ action loc31 ]

up [ action loc | action loc ] -> [ action loc HighUp | action loc HighDown ]
right [ action loc | action loc ] -> [ action loc HighRight | action loc HighLeft ]

[ lowertag ] [ moving Horizontal_ loc23 stationary HighUp ] -> [ lowertag ] [ moving Horizontal_ loc23 LowUp ]
[ lowertag ] [ moving Horizontal_ loc23 stationary HighDown ] -> [ lowertag ] [ moving Horizontal_ loc23 LowDown ]
[ lowertag ] [ moving Vertical_ loc23 stationary HighLeft ] -> [ lowertag ] [ moving Vertical_ loc23 LowLeft ]
[ lowertag ] [ moving Vertical_ loc23 stationary HighRight ] -> [ lowertag ] [ moving Vertical_ loc23 LowRight ]

[ lowertag ] [ moving Horizontal_ loc32 stationary HighUp ] -> [ lowertag ] [ moving Horizontal_ loc32 LowUp ]
[ lowertag ] [ moving Horizontal_ loc32 stationary HighDown ] -> [ lowertag ] [ moving Horizontal_ loc32 LowDown ]
[ lowertag ] [ moving Vertical_ loc32 stationary HighLeft ] -> [ lowertag ] [ moving Vertical_ loc32 LowLeft ]
[ lowertag ] [ moving Vertical_ loc32 stationary HighRight ] -> [ lowertag ] [ moving Vertical_ loc32 LowRight ]

[ lowertag ] [ loc23 moving Knot ] -> [ lowertag ] [ loc23 Knot ]
[ lowertag ] [ loc32 moving Knot ] -> [ lowertag ] [ loc32 Knot ]

[ loc23 moving HighUp ] -> [ loc23 LowUp ]
[ loc23 moving HighDown ] -> [ loc23 LowDown ]
[ loc23 moving HighLeft ] -> [ loc23 LowLeft ]
[ loc23 moving HighRight ] -> [ loc23 LowRight ]

[ loc32 moving HighUp ] -> [ loc32 LowUp ]
[ loc32 moving HighDown ] -> [ loc32 LowDown ]
[ loc32 moving HighLeft ] -> [ loc32 LowLeft ]
[ loc32 moving HighRight ] -> [ loc32 LowRight ]

[ moving Knot ] -> [ Knot ]

[ loc ] -> [ ]

[ lowertag ] -> [ ]


(((((((((((((((((((((((((((((((((())))))))))))))))))))))))))))))))))

late [ drag ] -> [ ]

late [ Tweak no Knot ] -> [ ]
late [ Tweak | Tweak CursorActive | Tweak ] -> [ Tweak | CursorActive | Tweak ] 

late [ Overlay ] -> [ ]
late [ High Low ] -> [ High Low Overlay ]

late [ LowUp no Overlay no Anchor ] -> [ HighUp ]
late [ LowDown no Overlay no Anchor ] -> [ HighDown ]
late [ LowLeft no Overlay no Anchor] -> [ HighLeft ]
late [ LowRight no Overlay no Anchor ] -> [ HighRight ]






(win conditions)
late [ CrateTarget Crate ] -> [ CrateTarget Crate WinConCrates ]
late [ BarrelTarget Barrel ] -> [ BarrelTarget Barrel WinConCrates ]

late [ CrossTarget Overlay ] -> [ CrossTarget Overlay WinConCrosses ]

late [ TargetCrossVertical Overlay HighUp ] -> [ TargetCrossVertical Overlay HighUp WinConRopes ]
late [ TargetCrossHorizontal Overlay HighLeft ] -> [ TargetCrossHorizontal Overlay HighLeft WinConRopes ]
late [ TargetUpDown HighUp HighDown ] -> [ TargetUpDown HighUp HighDown WinConRopes ]
late [ TargetLeftRight HighLeft HighRight ] -> [ TargetLeftRight HighLeft HighRight WinConRopes ] 
late [ TargetUpLeft HighUp HighLeft ] -> [ TargetUpLeft HighUp HighLeft WinConRopes ]
late [ TargetUpRight HighUp HighRight ] -> [ TargetUpRight HighUp HighRight WinConRopes ]
late [ TargetDownLeft HighDown HighLeft ] -> [ TargetDownLeft HighDown HighLeft WinConRopes ]
late [ TargetDownRight HighDown HighRight ] -> [ TargetDownRight HighDown HighRight WinConRopes ]

late [ Overlay no CrossTarget ] [ WinConCrosses ] -> [ Overlay ] [ ]

late [ Target no WinCon ] [ WinCon ] -> [ Target ] [ ]

late [ WinCon ] -> [ ] win



(iso decorations)
late [ Isometric ] -> [ ]

late up [ Crate | ] -> [ Crate | abovedecocrate ]
late up [ Barrel | ] -> [ Barrel | abovedecobarrel ]

late up [ CursorActive Crate | ] -> [ CursorCrate Crate | CursorCrateAbove ]
late up [ CursorActive Barrel | ] -> [ CursorBarrel Barrel | CursorBarrelAbove ]

(update decorations)
late [ Wall no WallDeco ] -> [ Wall random WallDeco ]

late [ LowUpDeco no LowUp ] -> [ ]
late [ LowDownDeco no LowDown ] -> [ ]
late [ LowLeftDeco no LowLeft ] -> [ ]
late [ LowRightDeco no LowRight ] -> [ ]
late [ HighUpDeco no HighUp ] -> [ ]
late [ HighDownDeco no HighDown ] -> [ ]
late [ HighLeftDeco no HighLeft ] -> [ ]
late [ HighRightDeco no HighRight ] -> [ ]

late [ LowUp_v1_0 LowDown ] -> [ LowUp_v1_0 LowDown LowDown_v1_0 ]
+ late [ LowUp_v1_1 LowDown ] -> [ LowUp_v1_1 LowDown LowDown_v1_1 ]
+ late [ LowDown_v1_0 LowUp ] -> [ LowDown_v1_0 LowUp LowUp_v1_0 ]
+ late [ LowDown_v1_1 LowUp ] -> [ LowDown_v1_1 LowUp LowUp_v1_1 ]
+ late [ LowLeft_v1_0 LowRight ] -> [ LowLeft_v1_0 LowRight LowRight_v1_0 ]
+ late [ LowLeft_v1_1 LowRight ] -> [ LowLeft_v1_1 LowRight LowRight_v1_1 ]
+ late [ LowRight_v1_0 LowLeft ] -> [ LowRight_v1_0 LowLeft LowLeft_v1_0 ]
+ late [ LowRight_v1_1 LowLeft ] -> [ LowRight_v1_1 LowLeft LowLeft_v1_1 ]
+ late [ HighUp_v1_0 HighDown ] -> [ HighUp_v1_0 HighDown HighDown_v1_0 ]
+ late [ HighUp_v1_1 HighDown ] -> [ HighUp_v1_1 HighDown HighDown_v1_1 ]
+ late [ HighUp_v1_0 HighLeft ] -> [ HighUp_v1_0 HighLeft HighLeft_v1_0 ]
+ late [ HighUp_v1_1 HighLeft ] -> [ HighUp_v1_1 HighLeft HighLeft_v1_1 ]
+ late [ HighUp_v1_0 HighRight ] -> [ HighUp_v1_0 HighRight HighRight_v1_0 ]
+ late [ HighUp_v1_1 HighRight ] -> [ HighUp_v1_1 HighRight HighRight_v1_1 ]
+ late [ HighDown_v1_0 HighUp ] -> [ HighDown_v1_0 HighUp HighUp_v1_0 ]
+ late [ HighDown_v1_1 HighUp ] -> [ HighDown_v1_1 HighUp HighUp_v1_1 ]
+ late [ HighDown_v1_0 HighLeft ] -> [ HighDown_v1_0 HighLeft HighLeft_v1_0 ]
+ late [ HighDown_v1_1 HighLeft ] -> [ HighDown_v1_1 HighLeft HighLeft_v1_1 ]
+ late [ HighDown_v1_0 HighRight ] -> [ HighDown_v1_0 HighRight HighRight_v1_0 ]
+ late [ HighDown_v1_1 HighRight ] -> [ HighDown_v1_1 HighRight HighRight_v1_1 ]
+ late [ HighLeft_v1_0 HighRight ] -> [ HighLeft_v1_0 HighRight HighRight_v1_0 ]
+ late [ HighLeft_v1_1 HighRight ] -> [ HighLeft_v1_1 HighRight HighRight_v1_1 ]
+ late [ HighLeft_v1_0 HighUp ] -> [ HighLeft_v1_0 HighUp HighUp_v1_0 ]
+ late [ HighLeft_v1_1 HighUp ] -> [ HighLeft_v1_1 HighUp HighUp_v1_1 ]
+ late [ HighLeft_v1_0 HighDown ] -> [ HighLeft_v1_0 HighDown HighDown_v1_0 ]
+ late [ HighLeft_v1_1 HighDown ] -> [ HighLeft_v1_1 HighDown HighDown_v1_1 ]
+ late [ HighRight_v1_0 HighLeft ] -> [ HighRight_v1_0 HighLeft HighLeft_v1_0 ]
+ late [ HighRight_v1_1 HighLeft ] -> [ HighRight_v1_1 HighLeft HighLeft_v1_1 ]
+ late [ HighRight_v1_0 HighUp ] -> [ HighRight_v1_0 HighUp HighUp_v1_0 ]
+ late [ HighRight_v1_1 HighUp ] -> [ HighRight_v1_1 HighUp HighUp_v1_1 ]
+ late [ HighRight_v1_0 HighDown ] -> [ HighRight_v1_0 HighDown HighDown_v1_0 ]
+ late [ HighRight_v1_1 HighDown ] -> [ HighRight_v1_1 HighDown HighDown_v1_1 ]
+ late right [ Right_v1_0 | HighLeft ] -> [ Right_v1_0 | HighLeft HighLeft_v1_1 ]
+ late right [ Right_v1_1 | HighLeft ] -> [ Right_v1_1 | HighLeft HighLeft_v1_0 ]
+ late right [ Right_v1_0 | LowLeft ] -> [ Right_v1_0 | LowLeft LowLeft_v1_1 ]
+ late right [ Right_v1_1 | LowLeft ] -> [ Right_v1_1 | LowLeft LowLeft_v1_0 ]
+ late left [ Left_v1_0 | HighRight ] -> [ Left_v1_0 | HighRight HighRight_v1_1 ]
+ late left [ Left_v1_1 | HighRight ] -> [ Left_v1_1 | HighRight HighRight_v1_0 ]
+ late left [ Left_v1_0 | LowRight ] -> [ Left_v1_0 | LowRight LowRight_v1_1 ]
+ late left [ Left_v1_1 | LowRight ] -> [ Left_v1_1 | LowRight LowRight_v1_0 ]
+ late down [ Down_v1_0 | HighUp ] -> [ Down_v1_0 | HighUp HighUp_v1_1 ]
+ late down [ Down_v1_1 | HighUp ] -> [ Down_v1_1 | HighUp HighUp_v1_0 ]
+ late down [ Down_v1_0 | LowUp ] -> [ Down_v1_0 | LowUp LowUp_v1_1 ]
+ late down [ Down_v1_1 | LowUp ] -> [ Down_v1_1 | LowUp LowUp_v1_0 ]
+ late up [ Up_v1_0 | HighDown ] -> [ Up_v1_0 | HighDown HighDown_v1_1 ]
+ late up [ Up_v1_1 | HighDown ] -> [ Up_v1_1 | HighDown HighDown_v1_0 ]
+ late up [ Up_v1_0 | LowDown ] -> [ Up_v1_0 | LowDown LowDown_v1_1 ]
+ late up [ Up_v1_1 | LowDown ] -> [ Up_v1_1 | LowDown LowDown_v1_0 ]

late [ LowUp_v2_0 LowDown ] -> [ LowUp_v2_0 LowDown LowDown_v2_0 ]
+ late [ LowUp_v2_1 LowDown ] -> [ LowUp_v2_1 LowDown LowDown_v2_1 ]
+ late [ LowDown_v2_0 LowUp ] -> [ LowDown_v2_0 LowUp LowUp_v2_0 ]
+ late [ LowDown_v2_1 LowUp ] -> [ LowDown_v2_1 LowUp LowUp_v2_1 ]
+ late [ LowLeft_v2_0 LowRight ] -> [ LowLeft_v2_0 LowRight LowRight_v2_0 ]
+ late [ LowLeft_v2_1 LowRight ] -> [ LowLeft_v2_1 LowRight LowRight_v2_1 ]
+ late [ LowRight_v2_0 LowLeft ] -> [ LowRight_v2_0 LowLeft LowLeft_v2_0 ]
+ late [ LowRight_v2_1 LowLeft ] -> [ LowRight_v2_1 LowLeft LowLeft_v2_1 ]
+ late [ HighUp_v2_0 HighDown ] -> [ HighUp_v2_0 HighDown HighDown_v2_0 ]
+ late [ HighUp_v2_1 HighDown ] -> [ HighUp_v2_1 HighDown HighDown_v2_1 ]
+ late [ HighUp_v2_0 HighLeft ] -> [ HighUp_v2_0 HighLeft HighLeft_v2_0 ]
+ late [ HighUp_v2_1 HighLeft ] -> [ HighUp_v2_1 HighLeft HighLeft_v2_1 ]
+ late [ HighUp_v2_0 HighRight ] -> [ HighUp_v2_0 HighRight HighRight_v2_0 ]
+ late [ HighUp_v2_1 HighRight ] -> [ HighUp_v2_1 HighRight HighRight_v2_1 ]
+ late [ HighDown_v2_0 HighUp ] -> [ HighDown_v2_0 HighUp HighUp_v2_0 ]
+ late [ HighDown_v2_1 HighUp ] -> [ HighDown_v2_1 HighUp HighUp_v2_1 ]
+ late [ HighDown_v2_0 HighLeft ] -> [ HighDown_v2_0 HighLeft HighLeft_v2_0 ]
+ late [ HighDown_v2_1 HighLeft ] -> [ HighDown_v2_1 HighLeft HighLeft_v2_1 ]
+ late [ HighDown_v2_0 HighRight ] -> [ HighDown_v2_0 HighRight HighRight_v2_0 ]
+ late [ HighDown_v2_1 HighRight ] -> [ HighDown_v2_1 HighRight HighRight_v2_1 ]
+ late [ HighLeft_v2_0 HighRight ] -> [ HighLeft_v2_0 HighRight HighRight_v2_0 ]
+ late [ HighLeft_v2_1 HighRight ] -> [ HighLeft_v2_1 HighRight HighRight_v2_1 ]
+ late [ HighLeft_v2_0 HighUp ] -> [ HighLeft_v2_0 HighUp HighUp_v2_0 ]
+ late [ HighLeft_v2_1 HighUp ] -> [ HighLeft_v2_1 HighUp HighUp_v2_1 ]
+ late [ HighLeft_v2_0 HighDown ] -> [ HighLeft_v2_0 HighDown HighDown_v2_0 ]
+ late [ HighLeft_v2_1 HighDown ] -> [ HighLeft_v2_1 HighDown HighDown_v2_1 ]
+ late [ HighRight_v2_0 HighLeft ] -> [ HighRight_v2_0 HighLeft HighLeft_v2_0 ]
+ late [ HighRight_v2_1 HighLeft ] -> [ HighRight_v2_1 HighLeft HighLeft_v2_1 ]
+ late [ HighRight_v2_0 HighUp ] -> [ HighRight_v2_0 HighUp HighUp_v2_0 ]
+ late [ HighRight_v2_1 HighUp ] -> [ HighRight_v2_1 HighUp HighUp_v2_1 ]
+ late [ HighRight_v2_0 HighDown ] -> [ HighRight_v2_0 HighDown HighDown_v2_0 ]
+ late [ HighRight_v2_1 HighDown ] -> [ HighRight_v2_1 HighDown HighDown_v2_1 ]
+ late right [ Right_v2_0 | HighLeft ] -> [ Right_v2_0 | HighLeft HighLeft_v2_1 ]
+ late right [ Right_v2_1 | HighLeft ] -> [ Right_v2_1 | HighLeft HighLeft_v2_0 ]
+ late right [ Right_v2_0 | LowLeft ] -> [ Right_v2_0 | LowLeft LowLeft_v2_1 ]
+ late right [ Right_v2_1 | LowLeft ] -> [ Right_v2_1 | LowLeft LowLeft_v2_0 ]
+ late left [ Left_v2_0 | HighRight ] -> [ Left_v2_0 | HighRight HighRight_v2_1 ]
+ late left [ Left_v2_1 | HighRight ] -> [ Left_v2_1 | HighRight HighRight_v2_0 ]
+ late left [ Left_v2_0 | LowRight ] -> [ Left_v2_0 | LowRight LowRight_v2_1 ]
+ late left [ Left_v2_1 | LowRight ] -> [ Left_v2_1 | LowRight LowRight_v2_0 ]
+ late down [ Down_v2_0 | HighUp ] -> [ Down_v2_0 | HighUp HighUp_v2_1 ]
+ late down [ Down_v2_1 | HighUp ] -> [ Down_v2_1 | HighUp HighUp_v2_0 ]
+ late down [ Down_v2_0 | LowUp ] -> [ Down_v2_0 | LowUp LowUp_v2_1 ]
+ late down [ Down_v2_1 | LowUp ] -> [ Down_v2_1 | LowUp LowUp_v2_0 ]
+ late up [ Up_v2_0 | HighDown ] -> [ Up_v2_0 | HighDown HighDown_v2_1 ]
+ late up [ Up_v2_1 | HighDown ] -> [ Up_v2_1 | HighDown HighDown_v2_0 ]
+ late up [ Up_v2_0 | LowDown ] -> [ Up_v2_0 | LowDown LowDown_v2_1 ]
+ late up [ Up_v2_1 | LowDown ] -> [ Up_v2_1 | LowDown LowDown_v2_0 ]


(debug - should never happen, but might be hard to notice if they do)
(if these are messages weird stuff happens, don't make them messages )


late [ HighUp LowUp ] -> [ Temp ]
late [ HighDown LowDown ] -> [ Temp ]
late [ HighLeft LowLeft ] -> [ Temp ]
late [ HighRight LowRight ] -> [ Temp ]

late [ HighUp LowDown ] -> [ Temp ]
late [ HighDown LowUp ] -> [ Temp ]
late [ HighLeft LowRight ] -> [ Temp ]
late [ HighRight LowLeft ] -> [ Temp ]

late right [ Right_ | no Left_ no Anchor ] -> [ Right_ | Temp ]
late left [ Left_ | no Right_ no Anchor ] -> [ Left_ | Temp ]
late down [ Down_ | no Up_ no Anchor ] -> [ Down_ | Temp ]
late up [ Up_ | no Down_ no Anchor ] -> [ Up_ | Temp ]


==============
WINCONDITIONS
==============

=======
LEVELS
=======

message All hands on deck! Click and drag crates onto the targets.

###########
###########
##...######
##.o.######
##...#.o.##
####*..*.##
##...#.o.##
##.*.######
##...######
###########
###########

message Click and drag on the rope to deform it. Ropes are blocked by crates and vice-versa!

###########
###########
##..╒─┐..##
##..│o│..##
##┌─╂─┘*.##
##│o│....##
##└─┘....##
##..*....##
##.......##
###########
###########


message Click a corner of the rope to wind it into a tight loop. Click the crossing of a tight loop to unwind it.

###########
###########
##╒┐..#####
##└┘..#####
##....#####
##.......##
#####....##
#####..+.##
#####....##
###########
###########

message Right click the rope to nail it down before dragging in order to create new bends.

###########
###########
####...####
####.*.####
##.╒───┐.##
##.│ooo│.##
##.└───┘.##
##.*.#.*.##
##...#...##
###########
###########

message placeholder message text goes here



###############
###############
##..╒──┐.....##
##..│*╓┿──┐..##
##..│.││.o│..##
##..│.││.o│..##
##..│*└╂──┘..##
##..└──┘.....##
###############
###############


(maybe replace this level?  I don't think it has much of a reason to be in the game at this point)
message Some levels contain cross targets. There must be a rope crossing on each target and no crossings anywhere else.

###############
###############
##...........##
##...........##
##...........##
##....╒──┐...##
##...╓╂─┐│...##
##.+.││.││.+.##
##...│└─╂┘...##
##...└──┘....##
##...........##
##...........##
##...........##
###############
###############

message placeholder message goes here

############
############
##╒─┐.....##
##│o│.....##
##└─╂──┐..##
##..│.*│..##
##..│*.│..##
##..└──┿─┐##
##.....│o│##
##.....└─┘##
############
############

message Barrels are like crates, but with fewer corners!

###########
###########
##..╒─┐..##
##..│U│..##
##┌─╂─┘*.##
##│o│....##
##└─┘....##
##.._....##
##.......##
###########
###########

#############
#############
##.........##
##...+.+...##
##..╒───┐..##
##.+│╓─┐│+.##
##..││.││..##
##.+│└─┘│+.##
##..└───┘..##
##...+.+...##
##.........##
#############
#############

#################
#################
##.............##
##.............##
##....╒──┐.....##
##....│┌─╂┐....##
##....││O││....##
##....│└┐││....##
##...┌┘┌┿┘│....##
##..┌╂─┿┿─┘....##
##..└╂─┘│......##
##...└──┘......##
##.............##
##.............##
##............*##
#################
#################

#############
#############
######╒───┐##
##...#│.O.│##
##...#└┐.┌┘##
##┌─┐##│.│###
##│*│##│.│###
##│.└──┿─┘###
##│O...│..###
##│.┌──┘..###
##│*│########
##└─┘########
##...########
##...########
#############
#############

#################
#################
##╒───┐###┌───┐##
##│...│###│...│##
##│...└─┐.│...│##
##│o._┌─╂─┘*.u│##
##│...│.└─┐...│##
##│...│###│...│##
##└───┘###└───┘##
#################
#################

#############
#############
##.........##
##....o....##
##..╒───┐..##
##..│╫═╫│..##
##..│║*║│..##
##..│╫═╫│..##
##..└───┘..##
##.........##
##.........##
#############
#############

#################
#################
##.............##
##........╒─┐..##
##........│┌╂┐.##
##.......┌┿╂┘│.##
##.......│└┿─┘.##
##.......└─┘...##
##.............##
##...╔═╗.......##
##.╔═╫╗║.......##
##.║╔╪╫╝.......##
##.╚╪╝║........##
##..╚═╝........##
##.............##
#################
#################

#####################
#####################
##.................##
##..........╒───┐..##
##..........│╓──┿┐.##
##.........┌╂┿─┐││.##
##.........│││.│││.##
##.........│└╂─┿┘│.##
##.........│.└─╂─┘.##
##.........└───┘...##
##.................##
##...╔═══╗.........##
##.╔═╪═╗.║.........##
##.║╔╫═╪╗║.........##
##.║║║.║║║.........##
##.║║╚═╫╪╝.........##
##.╚╫══╝║..........##
##..╚═══╝..........##
##.................##
#####################
#####################

(

#################
##.............##
##.............##
##.............##
##.............##
##.....╒┐......##
##....┌╂┿┐.....##
##....│└╂┘.....##
##....└─┘......##
##.............##
##.............##
##.............##
##.............##
##.............##
#################

#################
##.............##
##.............##
##...╒──┐......##
##...│.╓╂─┐....##
##...│╒╂┿┐│....##
##...└╂┿┘││....##
##....│└─┿┘....##
##....└──┘.....##
##.............##
##.............##
##.............##
##.............##
##.............##
#################

#############
#############
##.........##
##....o....##
##..╒───┐..##
##..│...│..##
##..i...1..##
##.........##
##.........##
##.........##
##.........##
#############
#############


#############
#############
##.........##
##....o....##
##..╒─┐....##
##..1.│.1..##
##....└─┘..##
##...+.....##
##.........##
##.........##
#############
#############

)